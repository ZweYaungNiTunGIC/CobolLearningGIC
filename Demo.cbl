000100 IDENTIFICATION DIVISION.
000200  PROGRAM-ID. DEMOCL04.
000300  AUTHOR. TOPICTRICK.
000400  DATE-WRITTEN.  01-JAN-2023.
000500  DATE-COMPILED. 01-JAN-2023.
000600*
000700* -----------------------------------------------------------*
000800*    PROJ: Demo Class 4.
000900*        DESC: This program demonstrate the following COBOL
001000*              statements.
001100*              1. COBOL Program Divisions.
001200*              2. COBOL Variables and Group Variables.
001300*              3. COBOL Initilialize Statement.
001400*              4. COBOL level 88 condition clause.
001500*              5. COBOL Redefine and Rename clause.
001600*              6. COBOL Accept and Display clause.
001700*
001800*        Files:
001900*              Input  - Employee Detail File (Sequential).
002000*              Output - Monthly Report       (Sequential).
002100*
002200*        Note: This program does not have any exception handling
002300*              error routine or file-status condition codes.
002400* -----------------------------------------------------------*
002500 ENVIRONMENT DIVISION.
002600  INPUT-OUTPUT SECTION.
002700  FILE-CONTROL.
002800     SELECT EMP-FILE ASSIGN TO  EMPCUR
002900            ORGANIZATION IS SEQUENTIAL.
003000*
003100     SELECT REP-FILE ASSIGN TO EMPRPT
003200            ORGANIZATION IS SEQUENTIAL.
003300*
003400 DATA DIVISION.
003500  FILE SECTION.
003600  FD EMP-FILE.
003700  COPY EMPREC REPLACING ==(EP)==  BY ==IN==.
003800*
003900  FD REP-FILE.
004000  01 REP-FILE-REC               PIC X(150).
004100*
004200 WORKING-STORAGE SECTION.
004300*
004400  01 WS-SWITCH.
004500     05 END-OF-FILE-SWITCH      PIC X(01)  VALUE 'N'.
004600        88 END-OF-FILE                     VALUE 'Y'.
004700        88 NOT-END-OF-FILE                 VALUE 'N'.
004800*
004900  01 WS-COUNTERS.
005000     05 WS-INP-REC              PIC 9(05).
005100     05 WS-OUT-REC              PIC 9(05).
005200*
005300  01 WS-TEMP-DATE.
005400     05 WS-HDTE                 PIC 9(08).
005500     05 THIS REDEFINES WS-HDTE.
005600        10 WS-HDTE-DD           PIC X(02).
005700        10 WS-HDTE-MM           PIC X(02).
005800        10 WS-HDTE-YYYY         PIC X(04).
005900     05 WS-TDY-DTE              PIC 9(08).
006000     05 THIS REDEFINES WS-TDY-DTE.
006100        10 WS-TDYDTE-YYYY       PIC 9(04).
006200        10 WS-TDYDTE-MM         PIC 9(02).
006300        10 WS-TDYDTE-DD         PIC 9(02).
006400
006500*
006600  01 HEAD1.
006700     05 FILLER            PIC X(60) VALUE SPACES.
006800     05 FILLER            PIC X(10) VALUE ' EMPLOYEE '.
006900     05 FILLER            PIC X(25) VALUE 'MANAGEMENT SYSTEM.'.
007000     05 FILLER            PIC X(41) VALUE SPACES.
007100     05 HD-DTE.
007200        10 HD-DTE-DD      PIC X(02) VALUE SPACES.
007300        10 FILLER         PIC X(01) VALUE '/'.
007400        10 HD-DTE-MM      PIC X(02) VALUE SPACES.
007500        10 FILLER         PIC X(01) VALUE '/'.
007600        10 HD-DTE-YYYY    PIC X(04) VALUE SPACES.
007700        10 HD-DOT         PIC X(01) VALUE '.'.
007800        10 HD-FILLER      PIC X(03) VALUE SPACES.
007900     66 WS-PGM-SUMY-DTE RENAMES HD-DTE-DD THRU HD-FILLER.
008000*
008100  01 HEAD2.
008200     05 FILLER            PIC X(70) VALUE SPACES.
008300     05 FILLER            PIC X(11) VALUE 'DUBLIN, IRL'.
008400     05 FILLER            PIC X(69) VALUE SPACES.
008500*
008600  01 COLHEAD3.
008700     05 FILLER            PIC X(04) VALUE SPACES.
008800     05 FILLER            PIC X(05) VALUE 'EMPNO'.
008900     05 FILLER            PIC X(02) VALUE SPACES.
009000     05 FILLER            PIC X(06) VALUE 'F NAME'.
009100     05 FILLER            PIC X(08) VALUE SPACES.
009200     05 FILLER            PIC X(07) VALUE 'M INIIT'.
009300     05 FILLER            PIC X(01) VALUE SPACES.
009400     05 FILLER            PIC X(06) VALUE 'L NAME'.
009500     05 FILLER            PIC X(11) VALUE SPACES.
009600     05 FILLER            PIC X(06) VALUE 'W DEPT'.
009700     05 FILLER            PIC X(03) VALUE SPACES.
009800     05 FILLER            PIC X(05) VALUE 'PH NO'.
009900     05 FILLER            PIC X(03) VALUE SPACES.
010000     05 FILLER            PIC X(06) VALUE 'H DATE'.
010100     05 FILLER            PIC X(05) VALUE SPACES.
010200     05 FILLER            PIC X(06) VALUE 'JOB TY'.
010300     05 FILLER            PIC X(04) VALUE SPACES.
010400     05 FILLER            PIC X(08) VALUE 'ED LEVEL'.
010500     05 FILLER            PIC X(02) VALUE SPACES.
010600     05 FILLER            PIC X(03) VALUE 'SEX'.
010700     05 FILLER            PIC X(02) VALUE SPACES.
010800     05 FILLER            PIC X(10) VALUE 'BIRTH DATE'.
010900     05 FILLER            PIC X(05) VALUE SPACES.
011000     05 FILLER            PIC X(06) VALUE 'SALARY'.
011100     05 FILLER            PIC X(05) VALUE SPACES.
011200     05 FILLER            PIC X(05) VALUE 'BONUS'.
011300     05 FILLER            PIC X(05) VALUE SPACES.
011400     05 FILLER            PIC X(04) VALUE 'COMM'.
011500     05 FILLER            PIC X(07) VALUE SPACES.
011600
011700* Report file detail line header.
011800
011900  01 DTL-LINE.
012000     05 FILLER               PIC X(03) VALUE SPACES.
012100     05 DTL-EMPNO            PIC Z(06).
012200     05 FILLER               PIC X(02) VALUE SPACES.
012300     05 DTL-FIRSTNME         PIC X(12).
012400     05 FILLER               PIC X(04) VALUE SPACES.
012500     05 DTL-MIDINIT          PIC X(01).
012600     05 FILLER               PIC X(05) VALUE SPACES.
012700     05 DTL-LASTNAME         PIC X(17).
012800     05 FILLER               PIC X(02) VALUE SPACES.
012900     05 DTL-WORKDEPT         PIC X(03).
013000     05 FILLER               PIC X(05) VALUE SPACES.
013100     05 DTL-PHONENO          PIC Z(04).
013200     05 FILLER               PIC X(02) VALUE SPACES.
013300     05 DTL-HIREDATE.
013400        10 DTL-HIREDATE-DD   PIC 9(02).
013500        10 FILLER            PIC X(01) VALUE '/'.
013600        10 DTL-HIREDATE-MM   PIC 9(02).
013700        10 FILLER            PIC X(01) VALUE '/'.
013800        10 DTL-HIREDATE-YYYY PIC 9(04).
013900     05 FILLER               PIC X(03) VALUE SPACES.
014000     05 DTL-JOB              PIC X(08).
014100     05 FILLER               PIC X(04) VALUE SPACES.
014200     05 DTL-EDLEVEL          PIC Z(02).
014300     05 FILLER               PIC X(06) VALUE SPACES.
014400     05 DTL-SEX              PIC X(01).
014500     05 FILLER               PIC X(03) VALUE SPACES.
014600     05 DTL-BIRTHDATE.
014700        10 DTL-BRTHDATE-DD   PIC 9(02).
014800        10 FILLER            PIC X(01) VALUE '/'.
014900        10 DTL-BRTHDATE-MM   PIC 9(02).
015000        10 FILLER            PIC X(01) VALUE '/'.
015100        10 DTL-BRTHDATE-YYYY PIC 9(04).
015200     05 FILLER               PIC X(03) VALUE SPACES.
015300     05 DTL-SALARY           PIC ZZZZZZ9.99.
015400     05 FILLER               PIC X(03) VALUE SPACES.
015500     05 DTL-BONUS            PIC ZZZ9.99.
015600     05 FILLER               PIC X(03) VALUE SPACES.
015700     05 DTL-COMM             PIC ZZZ9.
015800     05 FILLER               PIC X(07) VALUE SPACES.
015900*
016000 01 TRL-LINE.
016100     05 FILLER               PIC X(04)  VALUE SPACES.
016200     05 TRL-LINE-MSG         PIC X(27)  VALUE SPACES.
016300     05 TRL-COUNT            PIC 9(05)  VALUE ZEROES.
016400     05 FILLER               PIC X(114) VALUE SPACES.
016500*
016600 01 RPT-BLK-LNE.
016700    05 RPT-BLK-AST           PIC X(01)  VALUE '*'.
016800    05 RPT-BLK-SPC           PIC X(149) VALUE SPACES.
016900*
017000 PROCEDURE DIVISION.
017100 0000-CORE-BUSINESS-LOGIC.
017200     PERFORM A000-INIT-VALS
017300     PERFORM B000-OPEN-FILE
017400     PERFORM C000-PRNT-HDRS
017500     PERFORM D000-PROC-RECD
017600     PERFORM X000-CLSE-FILE
017700     STOP RUN.
017800*
017900 A000-INIT-VALS SECTION.
018000 A010-INIT-TMP-VALS.
018100     INITIALIZE WS-COUNTERS, DTL-LINE, TRL-LINE,
018200                WS-TEMP-DATE.
018300*
018400 A099-EXIT.
018500      EXIT.
018600*
018700 B000-OPEN-FILE SECTION.
018800 B010-OPEN-FILE.
018900      OPEN INPUT  EMP-FILE
019000           OUTPUT REP-FILE.
019100 B099-EXIT.
019200      EXIT.
019300*
019400 C000-PRNT-HDRS SECTION.
019500 C010-MOVE-TDY-DATE.
019600      ACCEPT WS-TDY-DTE    FROM DATE YYYYMMDD
019700      MOVE WS-TDYDTE-DD    TO HD-DTE-DD
019800      MOVE WS-TDYDTE-MM    TO HD-DTE-MM
019900      MOVE WS-TDYDTE-YYYY  TO HD-DTE-YYYY.
020000
020100 C020-PRNT-RPT-HDRS.
020200      WRITE REP-FILE-REC FROM HEAD1.
020300      WRITE REP-FILE-REC FROM HEAD2.
020400      WRITE REP-FILE-REC FROM RPT-BLK-LNE.
020500      WRITE REP-FILE-REC FROM COLHEAD3.
020600      WRITE REP-FILE-REC FROM RPT-BLK-LNE.
020700*
020800 C099-EXIT.
020900      EXIT.
021000*
021100 D000-PROC-RECD SECTION.
021200 D010-READ-FILE-REC.
021300     PERFORM UNTIL END-OF-FILE
021400         READ EMP-FILE
021500              AT END     SET END-OF-FILE TO TRUE
021600              NOT AT END PERFORM E000-PRNT-REPT
021700         END-READ
021800     END-PERFORM.
021900*
022000 D099-EXIT.
022100      EXIT.
022200*
022300 E000-PRNT-REPT SECTION.
022400 E010-MOVE-DTL-REC.
022500     MOVE  EMPNO             TO DTL-EMPNO
022600     MOVE  FIRSTNME          TO DTL-FIRSTNME
022700     MOVE  MIDINIT           TO DTL-MIDINIT
022800     MOVE  LASTNAME          TO DTL-LASTNAME
022900     MOVE  WORKDEPT          TO DTL-WORKDEPT
023000     MOVE  PHONENO           TO DTL-PHONENO
023100     MOVE  ZEROES            TO WS-HDTE
023200     MOVE  HIREDATE          TO WS-HDTE
023300     MOVE  WS-HDTE-DD        TO DTL-HIREDATE-DD
023400     MOVE  WS-HDTE-MM        TO DTL-HIREDATE-MM
023500     MOVE  WS-HDTE-YYYY      TO DTL-HIREDATE-YYYY
023600     MOVE  JOBTY             TO DTL-JOB
023700     MOVE  EDLEVEL           TO DTL-EDLEVEL
023800     MOVE  SEX               TO DTL-SEX
023900     MOVE  ZEROES            TO WS-HDTE
024000     MOVE  BIRTHDATE         TO WS-HDTE
024100     MOVE  WS-HDTE-DD        TO DTL-BRTHDATE-DD
024200     MOVE  WS-HDTE-MM        TO DTL-BRTHDATE-MM
024300     MOVE  WS-HDTE-YYYY      TO DTL-BRTHDATE-YYYY
024400     MOVE  SALARY            TO DTL-SALARY
024500     MOVE  BONUS             TO DTL-BONUS
024600     MOVE  COMM              TO DTL-COMM
024700     ADD +1                  TO WS-INP-REC.
024800*
024900 E020-WRITE-DTL-REC.
025000     WRITE REP-FILE-REC FROM DTL-LINE.
025100     INITIALIZE DTL-LINE
025200     ADD +1   TO WS-OUT-REC.
025300*
025400 E099-EXIT.
025500      EXIT.
025600*
025700 X000-CLSE-FILE SECTION.
025800 X010-PRNT-TRL-REC.
025900     WRITE REP-FILE-REC FROM RPT-BLK-LNE
026000*
026100     MOVE 'TOTAL NO OF RECORD READ  :' TO TRL-LINE-MSG
026200     MOVE  WS-INP-REC    TO TRL-COUNT
026300     WRITE REP-FILE-REC FROM TRL-LINE
026400*
026500     MOVE  SPACES        TO TRL-LINE-MSG
026600     MOVE  ZEROES        TO TRL-COUNT
026700*
026800     MOVE 'TOTAL NO OF RECORD PRINT :' TO TRL-LINE-MSG
026900     MOVE  WS-OUT-REC    TO TRL-COUNT
027000*
027100     WRITE REP-FILE-REC FROM TRL-LINE.
027200
027300 X020-CLOSE-FILE.
027400     CLOSE EMP-FILE, REP-FILE.
027500*
027600 X020-PRINT-TOTALS.
027700     DISPLAY '****** PROGRAM SUMMARY ****************'
027800     DISPLAY 'PGM EXECUTION DATE       :', WS-PGM-SUMY-DTE
027900     DISPLAY 'TOTAL NO OF RECORD READ  :', WS-INP-REC
028000     DISPLAY 'TOTAL NO OF RECORD PRINT :', WS-OUT-REC
028100     DISPLAY '****************************************'.
028200*
028300 X099-EXIT.
028400      EXIT.